OS X 特有の設定を行います。

#+BEGIN_SRC emacs-lisp
  (when (eq system-type 'darwin)
    (setq ns-function-modifier 'hyper)
    (setq ns-command-modifier 'meta)
    (setq ns-alternate-modifier 'super)

    ;; Menlo の場合は日本語にヒラギノが使われるので、
    ;; ascii に対して 1.2 倍サイズであることを明示する。
    (add-to-list 'face-font-rescale-alist '(".*Hiragino.*" . 1.2))

    (let* ((fonts '(
                    ("Myrica M for Powerline" . 140)
                    ("Myrica M" . 140)
                    ("Ricty Discord" . 135)
                    ("MyricaM M" . 140)
                    ("Menlo" . 120)
                    ))
           (font (cl-find-if (lambda (f) (find-font (font-spec :name (car f))))
                             fonts)))
      (when font
        (let ((font-family (car font))
              (font-height (cdr font)))
          (set-face-attribute 'default nil
                              :family font-family
                              :height font-height)
          (dolist (face '(variable-pitch fixed-pitch))
            (set-face-attribute face nil
                                :family font-family))
          (dolist (charset '(japanese-jisx0208 japanese-jisx0208 katakana-jisx0201))
            (set-fontset-font (frame-parameter nil 'font)
                              charset
                              (font-spec :family font-family))))))

    ;; これが無いと powerline のみためが悪い
    ;; cf. https://github.com/milkypostman/powerline/issues/54#issuecomment-65078550
    (setq ns-use-srgb-colorspace nil)

    ;; Enable emoji, and stop the UI from freezing when trying to display them.
    (if (fboundp 'set-fontset-font)
        (set-fontset-font t 'unicode "Apple Color Emoji" nil 'prepend))

    (let ((gls (executable-find "gls")))
      (when gls
        (setq insert-directory-program gls
              dired-listing-switches "-aBhl --group-directories-first"))))
#+END_SRC
